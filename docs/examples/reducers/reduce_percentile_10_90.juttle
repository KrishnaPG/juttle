// On historical data, you can apply reducer to get a single computation,
// or batch your historical data by time, then reduce per batch period.

sub historical_points() { 
  emit -from :0: -limit 10 
  | put value = Math.floor(Math.random() * 100)
}
historical_points 
| reduce pct10 = percentile(value, 0.1), pct90 = percentile(value, 0.9)
| view table -title "10th and 90th percentile of historical points"
;
historical_points 
| batch 5 
| reduce pct10 = percentile(value, 0.1), pct90 = percentile(value, 0.9) 
| view table 
    -update "append" 
    -title "10th and 90th percentile per 5-second batch, historical"
;

// On live streaming data, you must batch by time, then reduce per batch period.

sub live_points() { 
  emit -limit 10 
  | put value = Math.floor(Math.random() * 100)
}
live_points 
| batch 3 
| reduce pct10 = percentile(value, 0.1), pct90 = percentile(value, 0.9)
| view table 
    -update "append" 
    -title "10th and 90th percentile per 3-second batch, live"
